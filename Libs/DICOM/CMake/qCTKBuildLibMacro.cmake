
#
#
#
MACRO(qctk_build_qtlib)
  qCTK_PARSE_ARGUMENTS(MYQTLIB
    "NAME;EXPORT_TEMPLATE;EXPORT_DIRECTIVE;SRCS;MOC_SRCS;UI_FORMS;INCLUDE_DIRECTORIES;TARGET_LIBRARIES;RESOURCES;LIBRARY_TYPE"
    ""
    ${ARGN}
    )

  # Sanity checks
  IF(NOT DEFINED MYQTLIB_NAME)
    MESSAGE(SEND_ERROR "NAME is mandatory")
  ENDIF(NOT DEFINED MYQTLIB_NAME)
  IF(NOT DEFINED MYQTLIB_EXPORT_DIRECTIVE)
    MESSAGE(SEND_ERROR "EXPORT_DIRECTIVE is mandatory")
  ENDIF(NOT DEFINED MYQTLIB_EXPORT_DIRECTIVE)
  IF(NOT DEFINED MYQTLIB_EXPORT_TEMPLATE)
    MESSAGE(SEND_ERROR "EXPORT_TEMPLATE is mandatory")
  ENDIF(NOT DEFINED MYQTLIB_EXPORT_TEMPLATE)
  IF(NOT DEFINED MYQTLIB_LIBRARY_TYPE)
    SET(MYQTLIB_LIBRARY_TYPE "SHARED")
  ENDIF(NOT DEFINED MYQTLIB_LIBRARY_TYPE)

  # Define library name
  SET(lib_name ${MYQTLIB_NAME})

  # --------------------------------------------------------------------------
  # Include dirs
  SET(my_includes
    ${qCTK_BASE_INCLUDE_DIRS}
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_BINARY_DIR}
    ${MYQTLIB_INCLUDE_DIRECTORIES}
    )  
  SET(qCTK_BASE_INCLUDE_DIRS ${my_includes} CACHE INTERNAL "qCTK Base includes" FORCE)
  INCLUDE_DIRECTORIES(
    ${qCTK_BASE_INCLUDE_DIRS}
    )  
 
  SET(MY_LIBRARY_EXPORT_DIRECTIVE ${MYQTLIB_EXPORT_DIRECTIVE})
  SET(MY_EXPORT_HEADER_PREFIX ${MYQTLIB_NAME})
  SET(MY_LIBNAME ${lib_name})
  
  CONFIGURE_FILE(
    ${MYQTLIB_EXPORT_TEMPLATE}
    ${CMAKE_CURRENT_BINARY_DIR}/${MY_EXPORT_HEADER_PREFIX}Export.h
    )
  SET(dynamicHeaders
    "${dynamicHeaders};${CMAKE_CURRENT_BINARY_DIR}/${MY_EXPORT_HEADER_PREFIX}Export.h")

  QT4_WRAP_CPP(MYQTLIB_SRCS ${MYQTLIB_MOC_SRCS})
  QT4_WRAP_UI(MYQTLIB_UI_CXX ${MYQTLIB_UI_FORMS})
  SET(MYQTLIB_QRC_SRCS "")
  IF(DEFINED MYQTLIB_RESOURCES)
    QT4_ADD_RESOURCES(MYQTLIB_QRC_SRCS ${MYQTLIB_RESOURCES})
  ENDIF(DEFINED MYQTLIB_RESOURCES)

  SOURCE_GROUP("Resources" FILES
    ${MYQTLIB_RESOURCES}
    ${MYQTLIB_UI_FORMS}
    )

  SOURCE_GROUP("Generated" FILES
    ${MYQTLIB_MOC_SRCS}
    ${MYQTLIB_QRC_SRCS}
    ${MYQTLIB_UI_CXX}
    )
  
  ADD_LIBRARY(${lib_name} ${MYQTLIB_LIBRARY_TYPE}
    ${MYQTLIB_SRCS}
    ${MYQTLIB_UI_CXX}
    ${MYQTLIB_QRC_SRCS}
    )

  # Install rules
  #IF(qCTK_BUILD_SHARED_LIBS)
  #  INSTALL(TARGETS ${lib_name}
  #    RUNTIME DESTINATION ${qCTK_INSTALL_BIN_DIR} COMPONENT Runtime
  #    LIBRARY DESTINATION ${qCTK_INSTALL_LIB_DIR} COMPONENT Runtime
  #    ARCHIVE DESTINATION ${qCTK_INSTALL_LIB_DIR} COMPONENT Development)
  #ENDIF(qCTK_BUILD_SHARED_LIBS)
  
  SET(my_libs
    ${qCTK_BASE_LIBRARIES}
    ${MYQTLIB_TARGET_LIBRARIES}
    )
  TARGET_LINK_LIBRARIES(${lib_name} ${my_libs})

  # Update qCTK_BASE_LIBRARIES
  SET(qCTK_BASE_LIBRARIES ${my_libs} ${lib_name} CACHE INTERNAL "qCTK Base libraries" FORCE)  
  
  # Install headers
  #FILE(GLOB headers "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  #INSTALL(FILES
  #  ${headers}
  #  ${dynamicHeaders}
  #  DESTINATION ${qCTK_INSTALL_INCLUDE_DIR} COMPONENT Development
  #  )

ENDMACRO(qctk_build_qtlib)


